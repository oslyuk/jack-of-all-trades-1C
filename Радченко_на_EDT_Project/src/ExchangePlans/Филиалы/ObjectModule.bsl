Процедура ЗаписатьСообщениеСИзменениями() Экспорт
	
	СообщитьПользователю("-------- Выгрузка в узел " + Строка(ЭтотОбъект) + " ------------");
	
	Каталог = КаталогВременныхФайлов();
	
	// Сформировать имя временного файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(ПланыОбмена.Филиалы.ЭтотУзел().Код) + "_" +
		СокрЛП(Ссылка.Код) + ".xml";
			
	// Создать объект записи XML
	// *** ЗаписьXML-документов. 
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// *** Инфраструктура сообщений.
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка);
	СообщитьПользователю(" Номер сообщения: " + ЗаписьСообщения.НомерСообщения);
	
	// Получить выборку измененных данных
	// *** Механизм регистрации изменений.
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель,
		ЗаписьСообщения.НомерСообщения);
	Пока ВыборкаИзменений.Следующий() Цикл
		
		// Записать данные в сообщение.
		// *** XML-сериализация.
		ЗаписатьXML(ЗаписьXML, ВыборкаИзменений.Получить());
		
	КонецЦикла;
	
	ЗаписьСообщения.ЗакончитьЗапись();
	
	ЗаписьXML.Закрыть();
	
	СообщитьПользователю("-------- Конец выгрузки ------------");
	
КонецПроцедуры

Процедура ПрочитатьСообщениеСИзменениями() Экспорт
	
	Каталог = КаталогВременныхФайлов();
	
	// Сформировать имя файла.
	ИмяФайла = Каталог + "Message" + СокрЛП(Ссылка.Код) + "_" +
		СокрЛП(ПланыОбмена.Филиалы.ЭтотУзел().Код) + ".xml";
	
	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Возврат;
	КонецЕсли;
	
	// *** Чтение документов XML 
	// Попытаться открыть файл.
	ЧтениеXML = Новый ЧтениеXML();
	Попытка
		ЧтениеXML.ОткрытьФайл(ИмяФайла);
	Исключение
		
		СообщитьПользователю("Невозможно открыть файл обмена данными.");
		
		Возврат;
		
	КонецПопытки;
	
	СообщитьПользователю("-------- Загрузка из " + Строка(ЭтотОбъект) + " ------------");
	СообщитьПользователю(" – Считывается файл " + ИмяФайла);
	
	// Загрузить из найденного файла
	// *** Инфраструктура сообщений.
	ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
	
	// Читать заголовок сообщения обмена данными – файла XML.
	ЧтениеСообщения.НачатьЧтение(ЧтениеXML);
	
	// Сообщение предназначено не для этого узла.
	Если ЧтениеСообщения.Отправитель <> Ссылка Тогда
		ВызватьИсключение "Неверный узел";
	КонецЕсли;
	
	// Удалить регистрацию изменений для узла отправителя сообщения.
	// *** Служба регистрации изменений.
	ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);
	
	// Читать данные из сообщения *** XML-сериализация.
	Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		// Читать очередное значение.
		Данные = ПрочитатьXML(ЧтениеXML);
		
		// Не переносить изменение, полученное в главный из неглавного, если есть регистрация изменения.
		Если НЕ ЧтениеСообщения.Отправитель.Главный И
			ПланыОбмена.ИзменениеЗарегистрировано(ЧтениеСообщения.Отправитель, Данные) Тогда
			
			СообщитьПользователю(" – Изменения отклонены");
			
			Продолжить;
			
		КонецЕсли;
		
		// Записать полученные данные.
		Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
		Данные.ОбменДанными.Загрузка = Истина;
		Данные.Записать();
		
	КонецЦикла;
	
	ЧтениеСообщения.ЗакончитьЧтение();
	
	ЧтениеXML.Закрыть();
	
	УдалитьФайлы(ИмяФайла);
	
	СообщитьПользователю("-------- Конец загрузки ------------");
	
КонецПроцедуры

Функция СообщитьПользователю(ТекстСообщения)
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();
	
КонецФункции