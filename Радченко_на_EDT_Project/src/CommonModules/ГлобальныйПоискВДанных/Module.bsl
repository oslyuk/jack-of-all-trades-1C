Процедура ПоискДокументовПоНомеру(СтрокаПоиска, РезультатПоиска, ДопПараметры) Экспорт
	
	ФрагментНомера = Прав(СтрокаПоиска, СтрДлина(СтрокаПоиска) - 1);
	
	Если ФрагментНомера = "" Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОказаниеУслуги.Ссылка,
	|	ОказаниеУслуги.Номер,
	|	ОказаниеУслуги.Представление
	|ИЗ
	|	Документ.ОказаниеУслуги КАК ОказаниеУслуги
	|ГДЕ
	|	ОказаниеУслуги.Номер ПОДОБНО &ФрагментНомера";
	Запрос.УстановитьПараметр("ФрагментНомера", "%" + ФрагментНомера + "%");
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЭлементРезультата = Новый ЭлементРезультатаГлобальногоПоиска(Выборка.Ссылка,
			СтрНайтиИВыделитьОформлением(Выборка.Представление, ФрагментНомера));
		РезультатПоиска.Добавить(ЭлементРезультата);
		
	КонецЦикла;
	
КонецПроцедуры